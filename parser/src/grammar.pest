// -- Keywords
// TODO: valid identifiers that start with a reserved keyword are failing

keywords = _{
    if_
    | let_
    | const_
    | enum_
    | struct_
    | pub_
    | else_
    | not_
    | true_
    | false_
    | elif_
    | fn_
    | for_
}
    if_ = { "if" }
    const_ = { "const" }
    let_ = { "let" }
    enum_ = { "enum" }
    struct_ = { "struct" }
    pub_ = { "pub" }
    else_ = { "else" }
    not_ = { "not" }
    true_ = { "true" }
    false_ = { "false" }
    elif_ = { "elif" }
    fn_ = { "fn" }
    for_ = { "for" }
// -- Keywords

// Once WHITESPACE is defined, pest allows arbitrary whitespace between tokens
WHITESPACE = _{ (" " | NEWLINE) }

COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

// Silenced so that error messages show up as "expected identifier"
// instead of "expected alpha"
// TODO: add underline
alpha = _{ 'a'..'z' | 'A'..'Z' | "_" }
digit = { '0'..'9' }
arrow = { "->" }
hex = _{ '0'..'9' | 'a'..'f' | 'A'..'F' }
unicode_hex = { hex{1, 6} }
predefined = { "n" | "r" | "t" | "\\" | "0" | "\"" | "'" }
byte       = { "x" ~ hex{2} }
unicode    = { "u" ~ "{" ~ unicode_hex ~ "}" }
escape     = { "\\" ~ (predefined | byte | unicode) }


ascii_seq = @{ alpha ~ (alpha | digit)* }

// A valid identifier must not start with a digit
identifier = @{ !keywords ~ ascii_seq }

// An integer consists of one or more digits
// TODO: add support for underlines?
integer = @{
    ASCII_DIGIT+
}

// A float is two integers joined by a dot
float = {
    integer ~ "." ~ integer
}

num = { float | integer }

// A boolean value.
// Either true or false
boolean = _{
    True | False
}
    True = { "true" }
    False = { "false" }

// The `not` operator
not = {
    "not" ~ (identifier | boolean | curly_braced_expression | "(" ~ expression ~ ")" )
}

// The boolean binary operators
boolean_op = _{ and | or }
    and = { "and" }
    or = { "or" }

// A boolean expression: either a boolean value or a boolean operation
// TODO: allow for variables in boolean expressions
boolean_expr = {
    not
    | boolean_term ~ (boolean_op ~ boolean_term)*
}

// Comparison checks
// E.g.
//      x == y
comparison = {
    expression ~ comparison_operator ~ expression
}

// TODO: name these operators?
comparison_operator = { "==" | "!=" | ">=" | "<=" | ">" | "<" | "<="}

// A boolean term is either a boolean value (true or false)
// or a value that can be reduced to a boolean value (a boolean expression)
boolean_term = _{
    identifier | boolean | "(" ~ boolean_expr ~ ")"
}

if_expr = _{
    "if" ~ expression ~ curly_braced_expression ~ elif* ~ ("else" ~ curly_braced_expression)?
}
	curly_braced_expression = _{ "{" ~ expression ~ "}" }
    elif = _{ "elif" ~ expression ~ curly_braced_expression }

math_op = _{ add | subtract | multiply | divide | power | modulo }
    add      = { "+" }
    subtract = { "-" }
    multiply = { "*" }
    divide   = { "/" }
    power    = { "^" }
    modulo    = { "%" }

math_expr = {
    math_term ~ (math_op ~ math_term)*
}
math_term = _{ unary_minus | num | identifier | "(" ~ math_expr ~ ")" }

unary_minus = {
    "-" ~ (identifier | num | curly_braced_expression | "(" ~ expression ~ ")" )
}

// A char of the form 'x', where x is any value in char_value
char = {
    "'" ~ (escape | ANY) ~ "'"
}

raw_string = { (!("\\" | "\"") ~ ANY)+ }

// A string (sequence of characters), of the form "x.." where x is any value in char_value
string = {
    "\"" ~ (raw_string | escape)* ~ "\""
}

statement = {
    function_call
    | string
    | boolean_expr
    | math_expr
    | char
    | float
    | integer
    | boolean
    | assignment
    | if_expr
}


expression = {
    "{" ~ expression ~ "}"
    | statement ~ ";"
    | statement
}

// A Cacau program
program = _{
    SOI ~
    (function_definition | struct_definition | enum_definition | expression )+ ~
    EOI
}

// Type annotation of the form ": type_name"
type_annotation = { ":" ~ identifier }

// Binding a value to a name.
// May or may not contain a type annotation
// E.g.:
//     let x = "hey there"
//     let x: string = "hey there"
assignment = {
	"let" ~ identifier ~ type_annotation? ~ "=" ~ expression
}

// An argument for a function
// E.g.
//     x: int
//     name: string
function_argument = {
	identifier ~ type_annotation
}

// The return type for a function
// E.g.
//     -> bool
//     -> int
function_return = {
	arrow ~ identifier
}

// A variant of an enum
// E.g.
//     Password(ZeroableString)
//     Boolean(bool)
enum_variant = {
    identifier ~ "(" ~ identifier ~ ")" | identifier
}

/// A definition of an enum
///
/// E.g.
///     pub enum NameOrId { Name(string), Id(Uuid) }
///     enum MaybeString { Some(string), None }
enum_definition = {
    "pub"? ~ "enum" ~ identifier ~ "{" ~ (enum_variant ~ ",")* ~ enum_variant? ~ "}"
}

// The declaration of a function (without its body)
// E.g.:
//    fn main
//    pub fn multiply x: int, y: int -> int
//    fn is_even n: int -> bool
function_declaration = {
  "pub"? ~ "fn" ~ identifier ~ (function_argument ~ ",")* ~ function_argument? ~ function_return?
}

// The definition of a function
// E.g.:
//     fn is_even n: int -> bool { n % 2 == 0 }
//     pub fn is_zero n: int -> bool { n == 0 }
function_definition = {
    function_declaration ~ expression
}

// A field of a struct
// E.g.
//     pub username: string
//     age: int
struct_field = {
    "pub"? ~ identifier ~ ":" ~ identifier
}

// A definition of a struct
// E.g.
//     pub struct User { pub username: string, age: int, birth_date: Date }
struct_definition = {
    "pub"? ~ "struct" ~ identifier ~ "{" ~ (struct_field ~ ",")* ~ struct_field? ~ "}"
}

// The syntax of calling a function
function_call = {
    identifier ~ "(" ~ (expression ~ ",")* ~ expression? ~ ")"
}
